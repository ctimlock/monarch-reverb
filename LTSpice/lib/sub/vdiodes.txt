******************************************************
* Model generated on 30 Jan 2010
* Model format: LTspice
* Valve Diode or Rectifier macro model
* External node designations
* Node A -> anode
* Node K -> cathode
* No heater shown.
*
* Either single or double diode with separate cathodes,
* not here if full-wave rectifier with common cathode.
******************************************************

.SUBCKT VDIODE A K ; base generic = 6AL5
+PARAMS: Cak=3.2p Vpiv=330 Vmax=165
B1 A K I=0.0018 * uramp(V(A,K)) * sqrt(V(A,K))
D1 4 A vrzmax
D2 4 K vrzpiv
.model vrzpiv D
+Is=.6n Rs=.5 Cjo={Cak*2} nbv=6 bv={Vpiv}
+Ibv=1m Vpk={Vpiv} type=zener
.model vrzmax D
+Is=.6n Rs=.5 Cjo={Cak*2} nbv=6 bv={Vmax}
+Ibv=1m Vpk={Vmax} type=zener
.ENDS

.SUBCKT 6AL5 A K ; GEC
XV1 A K VDIODE
.ENDS

.SUBCKT 6D22S A K ; Svetlana
XV1 A K VDIODE
+PARAMS: Cak=12p Vpiv=6000 Vmax=2000
B1 A K I=0.006769 * uramp(V(A,K)) * sqrt(V(A,K))
.ENDS

.SUBCKT EB91 A K ; Philips, = EAA91
XV1 A K VDIODE
+PARAMS: Cak=3p Vpiv=420 Vmax=212
B1 A K I=0.0018 * uramp(V(A,K)) * sqrt(V(A,K))
.ENDS

.SUBCKT EAA91 A K ; Philips, = EB91
XV1 A K VDIODE
+PARAMS: Cak=3p Vpiv=420 Vmax=212
B1 A K I=0.0018 * uramp(V(A,K)) * sqrt(V(A,K))
.ENDS

.SUBCKT EB91a A K ; Philips, = EAA91
XV1 A K VDIODE
+PARAMS: Cak=0 Vpiv=420 Vmax=212
B1 A K I=0.0018 * uramp(V(A,K)) * sqrt(V(A,K))
.ENDS

