* Pentodes with active suppressor grids
* Warning!! Suppressor grid effect
* is a rough approximation only!

*****************************************
* Model generated on 22 Dec 2009
* updated 6 Jan 2010 using Norman Koren's
* new tetrode formulas.
* modified: 11 Feb 2010 (triode model where wired as triode)
* Model format: LTspice
* Pentode macro models (various where external suppressor pin)
* External node designations:
* Node A -> anode
* Node U -> suppressor grid
* Node S -> screen grid
* Node G -> signal grid
* Node K -> cathode
*
* MU = amplification factor.
* EX = line curvature, typical range from 1.3 to 1.4, increases with EX.
* kG1 = used to fit the equation to data for anode current.
* kG2 = similar to kG1 for screen current (pentode).
* KP (kP) behaviour region (A) (large negative grid voltage,
* large plate voltage, and low plate current).
* KVB "knee" of the characteristic curves, for positive bias.
*****************************************

.SUBCKT Pentode A U S G K ; base generic = EF86
+params: MU=34.90 EX=1.350 KG1=2648.1 KG2=4500 KP=222.06 KVB=4.7 VCT=0.00 RG1=2000 RG3=3000
+ CCG=4.3p cpg=0.6p CCP=5.1p

* Suppressor grid:
BIar Iar 0  V=if(V(U,K)-0.5, 1+V(U,K)*0.13, 1/((1+abs(V(U,K))*0.1)**1.5))
BIsr Isr 0  V=if(V(U,K)-0.5, 1/((1+V(U,K)*0.15)**1.5), 1+abs(V(U,K))*0.2)
*            ^ if Vg3 to k > 0, use the first calc., else use the second.
* The above are merely ratios by which the final anode and screen currents are
* multiplied to either increase or decrease by 1+ some fraction, as appropriate.

* copy of 'new' Norman Koren EF86 model:
RE1   E1 0  1MEG ; dummy so node E1 has two connections
Ee1   E1 0  Value={(V(S,K)/{kP}) * log(1+exp({kP} * (1/{Mu}+V(G,K)/V(S,K))))}

* Original 'new' lines:
* GIP   A  K  Value={(PWR(V(E1),EX)+PWRS(V(E1),EX))/KG1*ATAN(V(A,K)/KVB)}
* GIG2  S  K  Value={(EXP(EX*(LOG((V(S,K)/MU)+V(G,K)))))/KG2}

* Modified outputs:
GIP   A  K  Value={((PWR(V(E1),EX)+PWRS(V(E1),EX))/KG1*ATAN(V(A,K)/KVB))*V(Iar)}
GIG2  S  K  Value={((EXP(EX*(LOG((V(S,K)/MU)+V(G,K)))))/KG2)*V(Isr)}

* 'old' version:
* GIG2  S  K  Value={(pwr((V(G,K)+V(S,K)/{mu}),1.5)/{kG2})*V(Isr)}

* New lines ~ g1 & g3 should also have a diode behaviour:
R3 U 5 {RG3} ; For suppressor grid current
D3 5 K DX ; For suppressor grid diode
R1 G 6 {RG1} ; For signal grid current
D1 6 K DX ; For signal grid diode
Rak A K 1G
Rg3 U K 1G
Rg1 G K 1G

Cg1	G	K	{CCG}
Cak	A	K	{CCP}
Cg1a	G	A	{cpg}
Cg3	U	K	0.05p
Cg3a	U	A	2p
.MODEL DX D(Ron=1 Roff=1G Vfwd=.75)
.ends

.SUBCKT Pentode2 A U S G K
* only used by EF80, especially for FM tuner IF amplifier, capacitances disabled.
+params: MU=49.11 EX=1.420 KG1=350 KG2=560 KP=375.96 KVB=42.9 VCT=0.50 RG1=100 RG3=1000
+ CCG=2.6p cpg=0.007p CCP=3.3p

* Suppressor grid:
BIar Iar 0  V=if(V(U,K)-0.5, 1+V(U,K)*0.13, 1/((1+abs(V(U,K))*0.1)**1.5))
BIsr Isr 0  V=if(V(U,K)-0.5, 1/((1+V(U,K)*0.15)**1.5), 1+abs(V(U,K))*0.2)
*            ^ if Vg3 to k > 0, use the first calc., else use the second.
* The above are merely ratios by which the final anode and screen currents are
* multiplied to either increase or decrease by 1+ some fraction, as appropriate.

* copy of 'new' Norman Koren model:
RE1   E1 0  1MEG ; dummy so node E1 has two connections
Ee1   E1 0  Value={(V(S,K)/{kP}) * log(1+exp({kP} * (1/{Mu}+V(G,K)/V(S,K))))}

* Original 'new' lines:
* GIP   A  K  Value={(PWR(V(E1),EX)+PWRS(V(E1),EX))/KG1*ATAN(V(A,K)/KVB)}
* GIG2  S  K  Value={(EXP(EX*(LOG((V(S,K)/MU)+V(G,K)))))/KG2}

* Modified outputs:
* GIP   A  K  Value={((PWR(V(E1),EX)+PWRS(V(E1),EX))/KG1*ATAN(V(A,K)/KVB))*V(Iar)}
GIP   A  K  VALUE={pi/2*(PWR(V(E1),EX)+PWRS(V(E1),EX))/KG1*(1-exp(-2*V(A,K)/(pi*KVB)))*V(Iar)}
* ^ modification for EF80 - not mine!
GIG2  S  K  Value={((EXP(EX*(LOG((V(S,K)/MU)+V(G,K)))))/KG2)*V(Isr)}

* 'old' version:
* GIG2  S  K  Value={(pwr((V(G,K)+V(S,K)/{mu}),1.5)/{kG2})*V(Isr)}

* New lines ~ g1 & g3 should also have a diode behaviour:
R3 U 5 {RG3} ; For suppressor grid current
D3 5 K DX ; For suppressor grid diode
R1 G 6 {RG1} ; For signal grid current
D1 6 K DX ; For signal grid diode
Rak A K 1G
Rg3 U K 1G
Rg1 G K 1G

* Cg1	G	K	{CCG} ; }
* Cak	A	K	{CCP} ; } no capacitances! for RF usage
* Cg1a	G	A	{cpg} ; }
* Cg3	U	K	0.05p ; }
* Cg3a	U	A	2p ; }
.MODEL DX D(Ron=1 Roff=1G Vfwd=.75)
.ends


.SUBCKT Pentode3 A U S G K
* only used by EF89, especially for FM tuner IF amplifier, capacitances disabled.
+params: MU=49.11 EX=1.420 KG1=350 KG2=560 KP=375.96 KVB=42.9 VCT=0.50 RG1=100 RG3=1000
+ CCG=2.6p cpg=0.007p CCP=3.3p

* Suppressor grid:
BIar Iar 0  V=if(V(U,K)-0.5, 1+V(U,K)*0.13, 1/((1+abs(V(U,K))*0.1)**1.5))
BIsr Isr 0  V=if(V(U,K)-0.5, 1/((1+V(U,K)*0.15)**1.5), 1+abs(V(U,K))*0.2)
*            ^ if Vg3 to k > 0, use the first calc., else use the second.
* The above are merely ratios by which the final anode and screen currents are
* multiplied to either increase or decrease by 1+ some fraction, as appropriate.

* This bit provides variable mu depending on Vgk:
Ee2   E2 0  K G  table=(0 30.52, 20 2.1) ; < this is EF89 mu, can't use variables here!

* copy of 'new' Norman Koren model:
RE1   E1 0  1MEG ; dummy so node E1 has two connections
Ee1   E1 0  Value={(V(S,K)/{kP}) * log(1+exp({kP} * (1/V(E2)+V(G,K)/V(S,K))))}

* Original 'new' lines:
* GIP   A  K  Value={(PWR(V(E1),EX)+PWRS(V(E1),EX))/KG1*ATAN(V(A,K)/KVB)}
* GIG2  S  K  Value={(EXP(EX*(LOG((V(S,K)/V(E2))+V(G,K)))))/KG2}

* Modified outputs:
* GIP   A  K  Value={((PWR(V(E1),EX)+PWRS(V(E1),EX))/KG1*ATAN(V(A,K)/KVB))*V(Iar)}
GIP   A  K  VALUE={pi/2*(PWR(V(E1),EX)+PWRS(V(E1),EX))/KG1*(1-exp(-2*V(A,K)/(pi*KVB)))*V(Iar)}
* ^ modification for EF80 - not mine!
GIG2  S  K  Value={((EXP(EX*(LOG((V(S,K)/V(E2))+V(G,K)))))/KG2)*V(Isr)}

* 'old' version:
* GIG2  S  K  Value={(pwr((V(G,K)+V(S,K)/V(E2)),1.5)/{kG2})*V(Isr)}

* New lines ~ g1 & g3 should also have a diode behaviour:
R3 U 5 {RG3} ; For suppressor grid current
D3 5 K DX ; For suppressor grid diode
R1 G 6 {RG1} ; For signal grid current
D1 6 K DX ; For signal grid diode
Rak A K 1G
Rg3 U K 1G
Rg1 G K 1G

* Cg1	G	K	{CCG} ; }
* Cak	A	K	{CCP} ; } no capacitances! for RF usage
* Cg1a	G	A	{cpg} ; }
* Cg3	U	K	0.05p ; }
* Cg3a	U	A	2p ; }
.MODEL DX D(Ron=1 Roff=1G Vfwd=.75)
.ends


.SUBCKT EF86 A U S G K ; duplicate of generic
XV1 A U S G K Pentode
.ENDS

.SUBCKT EF36 A U S G K ; duplicate of generic
XV1 A U S G K Pentode
.ENDS

.SUBCKT EF37A A U S G K
XV1 A U S G K Pentode
+params: MU=37
.ENDS

.subckt EL34 A U S G K ; as true pentode operation
* Mullard Data Book Apr 1962 
XV1 A U S G K Pentode
+params: MU=12.02 EX=1.169 KG1=353.9 KG2=4500 KP=61.11 KVB=29.9 VCT=0.00 RG1=1000 CCG=15.0p cpg=1.0p CCP=8.0p
.ends

.subckt EF80 A U S G K ; screen edited!
* Philips data sheet AKA 6BX6
XV1 A U S G K Pentode2
+params: MU=49.11 EX=1.420 KG1=350 KG2=560 KP=375.96 KVB=42.9 VCT=0.50 RGI=100 CCG=2.6p cpg=0.007p CCP=3.3p
.ends

.subckt EF89 A U S G K ; variable mu pentode ; screen edited!
* Philips data sheet AKA 6DA6
XV1 A U S G K Pentode3
+params: MU=30.52 EX=1.350 KG1=1110 KG2=460 KP=39.49 KVB=23.6 VCT=0.00 RGI=100 CCG=5.5p cpg=0.002p CCP=3.2p
* CCP=5.1p
.ends


********************************************
* Use this part for where is wired as triode
* (screen to anode). In this implementation
* screen and suppressor grids have no effect
* but are just included to make the schematic
* drawing complete showing all terminals.
* Note in this case param kg2 is ignored.
********************************************

.subckt PentodeT A U S G K ; generic = EF86
+params: mu=38 ex=1.5 kg1=1400 kg2=0 kp=250 kvb=300 vct=0.00 rgi=2000 rg3=3000 ccg=4.3p cpg=0.6p ccp=5.1p

* (^ mu has to be 38 else grid lines won't 'stretch out' properly)

RE1   E1 0  1MEG ; dummy so node E1 has two connections
Ee1   E1 0  Value {(V(A,K)/{kP}) * log(1 + exp({kP} * (1/{mu} + (V(G,K)+VCT)/sqrt({kVB} + pwr(V(A,K),2)))))}
GIP   A  K  Value {(pwr(V(E1),EX) + pwrs(V(E1),EX))/KG1} ; Norman Koren's

R1    G  5  {rgi} ; for grid forward current
D1    5  K  DX ; for grid diode action
R3    U  6  {RG3} ; For suppressor grid current
D3    6  K  DX ; For suppressor grid diode
CM1   G  K  {ccg}
CM2   A  G  {cpg}
CM3   A  K  {ccp}
Cg3   U  K  0.05p
Cg3a  U  A  2p
RF1   A  0  1G
Rg3   U  K  1G
Rg2   S  K  1G ; prevent floating nodes
RF2   G  0  1G
RF3   K  0  1G
.MODEL DX D(Ron=1 Roff=1G Vfwd=.75)
.ends


.SUBCKT EF86_at A U S G K ; wired as triode
XV1 A U S G K PentodeT
.ENDS


.SUBCKT EF36_at A U S G K ; wired as triode
XV1 A U S G K PentodeT
.ENDS


.SUBCKT EF37A_at A U S G K ; wired as triode
XV1 A U S G K PentodeT
.ENDS


.subckt EL34_at A U S G K ; wired as triode
XV1 A U S G K PentodeT
+params: mu=11.17 ex=1.48 kg1=597 kp=40.5 kvb=24.6 cpg=1.1p ccg=15p ccp=8.5p rgi=1000
.ends

.subckt 6AC7_at A U S G K ; [graph by Tom Schlangen]
XV1 A U S G K PentodeT
+params: mu=50 ex=1.1 kg1=200 kp=340 kvb=300 ccg=11p cpg=0.015p ccp=5p
.ends


